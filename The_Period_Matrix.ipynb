{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Note that the labelling of these sections is based on the version of the paper completed on 15/08/22. This cell should be updated in the event of a change to the labelling in the paper.\n",
    "\n",
    "Currently running on Sagemath version 9.4.rc0, Release Date: 2021-07-27.\n",
    "Using Python 3.9.5.\n",
    "\n",
    "This notebook is intended to be ran sequentially from the beginning. Changing the order in which cells are ran may lead to errors. "
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Note in the following we will often take a value being less than $10^{-10}$ to be sufficient to consider it as zero. This is arbitrary and not connected to our choice of precision. If one wished to be precise an investigation of how any error decreases as precision is increased would be appropriate. "
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Theorem 2.12"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "t0: 0.50000000000000000 + 0.18667646246280991*I \n",
      "\n",
      "[ 0  0  0  1  0  0  1  2]\n",
      "[ 0 -1  1  0 -1 -2  1  1]\n",
      "[ 1  0  1  0  2  1  1  1]\n",
      "[ 0  1  0  0  1  2  0  0]\n",
      "[ 1  0  0  0  2  1  0  1]\n",
      "[ 1  0  1 -1  1  1  1 -1]\n",
      "[-1  0  0  1 -1 -1  1  2]\n",
      "[ 1  0  1 -1  1  2  1 -1] \n",
      "\n"
     ]
    }
   ],
   "source": [
    "# We will first want to verify the claims about Weber's period matrix. \n",
    "# To do this we will go via the Riemann matrix calculated by Sage. \n",
    "from sage.schemes.riemann_surfaces.riemann_surface import numerical_inverse, integer_matrix_relations\n",
    "from sage.schemes.riemann_surfaces.riemann_surface import RiemannSurface\n",
    "\n",
    "# Let's define the curve\n",
    "R.<x, y> = QQ[]\n",
    "f = x*(y^5+1) + (x*y)^2 - x^4*y - 2*y^3\n",
    "integration_method = 'rigorous'\n",
    "prec = 60\n",
    "S = RiemannSurface(f, prec=prec, integration_method=integration_method)\n",
    "g = S.genus\n",
    "\n",
    "# To relate this to the R&R model we will need to calculate tau_0 := t0 \n",
    "# to the desired precision. We will do this using the hypergeometric function\n",
    "# to invert. \n",
    "j0 = -29^3*5*2^(-5)\n",
    "alp = S._CCz.gen()\n",
    "roots = (4*j0*alp*(1-alp)-1728).roots(multiplicities=False)\n",
    "# Note one could use the Arb's hypergeometric to get rigorous bounds on these, \n",
    "# but that won't be necessary for us\n",
    "t1, t2 = (I*hypergeometric([1/6,5/6],[1],1-al).hypergeometric_simplify()/\\\n",
    "          hypergeometric([1/6,5/6],[1],al).hypergeometric_simplify() \n",
    "          for al in roots)\n",
    "\n",
    "# We need to choose the right root, which for us will have real part 0.5. \n",
    "if (t1.real_part()-1/2).abs()>1e-10:\n",
    "    t1 = t2\n",
    "if (t1.real_part()-1/2).abs()>1e-10:\n",
    "    raise ValueError(\"Invalid taus found, check calculation.\")\n",
    "    \n",
    "# We will then map this to the desired form of t0 using a Mobius transform\n",
    "from sage.geometry.hyperbolic_space.hyperbolic_model import moebius_transform\n",
    "t0 = moebius_transform(matrix(2,2,[-1,1,-2,1]), S._CC(t1))\n",
    "# Check it is valid\n",
    "if (elliptic_j(t0)+29^3*5/2^5).norm()+(elliptic_j(5*t0)+25/2).norm()>1e-10:\n",
    "    raise ValueError(\"Value of t0 found doesn't satisfy the desired conditions.\")\n",
    "# Here we will now print t0 to be concrete about which value we have taken. \n",
    "print(\"t0:\", t0, \"\\n\")\n",
    "# This let's us define the R&R Riemann matrix. \n",
    "RM_Braden = t0*matrix([[4,1,-1,1],[1,4,1,-1],[-1,1,4,1],[1,-1,1,4]])\n",
    "\n",
    "# We now want to define Weber's period matrix wrt to a canonical homology basis. \n",
    "# We write down the intersection matrxi Weber gives. \n",
    "Int_matrix = matrix([[0,1,1,-1,-1,0,0,1],\n",
    "                    [-1,0,1,1,0,1,0,-1],\n",
    "                    [-1,-1,0,1,0,-1,0,0],\n",
    "                    [1,-1,-1,0,0,0,1,0],\n",
    "                    [1,0,0,0,0,1,1,-1],\n",
    "                    [0,-1,1,0,-1,0,1,1],\n",
    "                    [0,0,0,-1,-1,-1,0,1],\n",
    "                    [-1,1,0,0,1,-1,-1,0]])\n",
    "\n",
    "# We canonicalise it. \n",
    "from sage.matrix.symplectic_basis import symplectic_basis_over_ZZ\n",
    "C = symplectic_basis_over_ZZ(Int_matrix)[1].transpose()\n",
    "\n",
    "# We construct Weber's period matrix.\n",
    "xi = exp(S._RR.pi()*I/5)\n",
    "Phi = (S._RR(sqrt(5))+1)/2\n",
    "# To get l we invert the final expression in Corollary 5.3 of Weber, using the fact that \n",
    "# we can minimise to find the approximation to a reliable accuracy. \n",
    "def loss_fn(t):\n",
    "    tau = CC(t[0]+I*t[1])\n",
    "    j1 = elliptic_j(tau)\n",
    "    return (j1+25/2).abs()^2\n",
    "minxy = minimize(loss_fn, [CC(0.554051), CC(0.832482)], xtol=1e-60)\n",
    "trat = CC(minxy[0]+minxy[1]*I)\n",
    "\n",
    "l = (xi*xi.conjugate()*(1-xi)-\n",
    "     trat*xi.conjugate()*(1-xi.conjugate()))/(xi.conjugate()-trat)\n",
    "PM_list = [[xi^(2*k)*(1-xi^4), xi^(4*k+7)*(1-xi^8)*(l*Phi-1),\n",
    "           xi^(8*k+6)*(1-xi^6)*Phi*(1-l), xi^(6*k+4)*(1-xi^2)*l] \n",
    "          for k in range(4)]\n",
    "PM_list += [[xi^(4*k+7)*(1-xi^8)*(l*Phi-1), \n",
    "             xi^(8*k+6)*(1-xi^6)*Phi*(1-l),\n",
    "             xi^(6*k+4)*(1-xi^2)*l, xi^(2*k)*(1-xi^4)] \n",
    "          for k in range(4)]\n",
    "PM_W = Matrix(8, 4, PM_list).transpose()\n",
    "PM_W = PM_W*C\n",
    "A_W = PM_W[:,0:g]\n",
    "B_W = PM_W[:,g:]\n",
    "RM_W = numerical_inverse(A_W)*B_W\n",
    "\n",
    "# We now use code written by NB to get the symplectic automorphism group to find the relations\n",
    "# between the two \n",
    "Rs = integer_matrix_relations(RM_W, RM_Braden)\n",
    "r = len(Rs)\n",
    "A = PolynomialRing(QQ, r, 'x')\n",
    "gensA = A.gens()\n",
    "        # Use that the trace is positive definite; we could also put this as an\n",
    "        # extra condition when determining the endomorphism basis to speed up\n",
    "        # that calculation slightly\n",
    "R = sum(gensA[i] * Rs[i].change_ring(A) for i in range(r))\n",
    "tr = (R * S.rosati_involution(R)).trace()\n",
    "        # Condition tr = 2 g creates ellipsoid\n",
    "M = Matrix(ZZ, r, r, [tr.derivative(gen1).derivative(gen2)\n",
    "                        for gen1 in gensA for gen2 in gensA])\n",
    "\n",
    "vs = M.__pari__().qfminim(4*g)[2].sage().transpose()\n",
    "\n",
    "vs = [v for v in vs if v * M * v == 4*g]\n",
    "vs += [-v for v in vs]\n",
    "RsIso = []\n",
    "for v in vs:\n",
    "    R = sum(v[i] * Rs[i] for i in range(r))\n",
    "    if R * S.rosati_involution(R) == 1:\n",
    "        RsIso.append(R)\n",
    "\n",
    "if len(RsIso)!=240:\n",
    "    raise ValueError(\"Period matrices do not give isomorphic Jacobians, error present.\")\n",
    "    \n",
    "# We pick out an element which gives an isomorphism with few non-zero entries.\n",
    "# This may not be the exact matrix in the paper as the precision used will affect which matrix \n",
    "# is printed. \n",
    "non_zeros = [sum([bool(ri) for ri in RIs.list()]) for RIs in RsIso]\n",
    "min_ind = non_zeros.index(min(non_zeros))\n",
    "R0 = RsIso[min_ind]\n",
    "print(R0,\"\\n\")\n",
    "D0 = R0[0:g, 0:g]\n",
    "B0 = R0[0:g, g:]\n",
    "C0 = R0[g:, 0:g]\n",
    "A0 = R0[g:, g:]\n",
    "if (max([el.norm() for el in (B0+RM_W*A0-(D0+RM_W*C0)*RM_Braden).list()]) > 1e-10):\n",
    "    raise ValueError(\"Error in transform. \")\n",
    "    \n",
    "R_paper = matrix([[0, 0, 0, -1, 1, -1, 0, 2],\n",
    "                 [0, -1, 1, -1, 1, 1, -1, 1],\n",
    "                 [1, 0, 1, -1, -1, -1, -1, 1],\n",
    "                 [1, 0, 0, 0, -2, 0, 0, -1],\n",
    "                 [0, 1, 0, 0, 0, -2, -1, 0],\n",
    "                 [1, 0, 1, 0, -2, -1, -1, -1],\n",
    "                 [0, -1, 0, -1, 1, 2, 1, 1],\n",
    "                 [1, 0, 1, 0, -1, -1, -2, -1]])\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "True\n"
     ]
    }
   ],
   "source": [
    "# To show that the R&R period matrix is valid straight from Sage, we can do the above but\n",
    "# find a transform from the numerically calculated Riemann matrix. \n",
    "RM = S.riemann_matrix()\n",
    "\n",
    "Rs = integer_matrix_relations(RM, RM_Braden)\n",
    "r = len(Rs)\n",
    "A = PolynomialRing(QQ, r, 'x')\n",
    "gensA = A.gens()\n",
    "        # Use that the trace is positive definite; we could also put this as an\n",
    "        # extra condition when determining the endomorphism basis to speed up\n",
    "        # that calculation slightly\n",
    "R = sum(gensA[i] * Rs[i].change_ring(A) for i in range(r))\n",
    "tr = (R * S.rosati_involution(R)).trace()\n",
    "        # Condition tr = 2 g creates ellipsoid\n",
    "M = Matrix(ZZ, r, r, [tr.derivative(gen1).derivative(gen2)\n",
    "                        for gen1 in gensA for gen2 in gensA])\n",
    "\n",
    "vs = M.__pari__().qfminim(4*g)[2].sage().transpose()\n",
    "\n",
    "vs = [v for v in vs if v * M * v == 4*g]\n",
    "vs += [-v for v in vs]\n",
    "RsIso = []\n",
    "for v in vs:\n",
    "    R = sum(v[i] * Rs[i] for i in range(r))\n",
    "    if R * S.rosati_involution(R) == 1:\n",
    "        RsIso.append(R)\n",
    "\n",
    "print(len(RsIso)==240)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SageMath 9.4.rc0",
   "language": "sage",
   "name": "sagemath"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.9.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
